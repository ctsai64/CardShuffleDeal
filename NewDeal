#include <AccelStepper.h>
#include "Adafruit_LiquidCrystal.h"

// =======================================================
//                     PIN ASSIGNMENTS
// =======================================================

// NEW PIN MAPPING (2-pin motors)
const int motorL1 = 2;   // Left shuffle forward
const int motorL2 = 3;   // Left shuffle backward

const int motorR1 = 4;   // Right shuffle forward
const int motorR2 = 5;   // Right shuffle backward

const int motorD1 = 9;   // Deal forward
const int motorD2 = 8;   // Deal backward

const int motorS1 = 6;   // Shooter forward
const int motorS2 = 7;   // Shooter backward

/*
 // Stepper 28BYJ-48 + ULN2003
 #define IN1 10
 #define IN2 11
 #define IN3 12
 #define IN4 13
*/

const int butPlayUp   = 53;
const int butPlayDown = 52;
const int butCardUp   = 49;
const int butCardDown = 48;
const int butStart    = 23;

// Photoresistors
const int phoRight = A0;
const int phoLeft  = A1;

// Random seed
const int randPin = A3;

// =======================================================
//                     CONSTANTS
// =======================================================

const unsigned long shuffleRunTime   = 300;
const unsigned long shuffleDelay = 1000;

const unsigned long dealForwardTime  = 50;
const unsigned long dealDelay  = 35;
const unsigned long dealBackwardTime = 200;

const int sensorThreshold = 1;

int numPlayers = 4;
int numCards   = 52;

// LCD
Adafruit_LiquidCrystal lcd(0);

// =======================================================
//                     STEPPER
// =======================================================

const int STEP_PIN = 45;
const int DIR_PIN  = 44;
const int ENABLE_PIN = 43; // LOW = enabled, HIGH = disabled

// Optional microstepping pins
const int MS1_PIN = 41;
const int MS2_PIN = 39;
const int MS3_PIN = 37;

const int STEPS_PER_REV = 100;   // full steps
const int MICROSTEPPING = 16;      // 1, 2, 4, 8, or 16 depending on MS1–MS3 wiring
const int STEP_DELAY_US = 800;     // speed (bigger = slower = less current/heat)

// =======================================================
//                   MOTOR HELPERS
// =======================================================

void motorForward(int pin1, int pin2, unsigned long t) {
  digitalWrite(pin1, HIGH);
  digitalWrite(pin2, LOW);
  delay(t);
  digitalWrite(pin1, LOW);
  digitalWrite(pin2, LOW);
}

void motorBackward(int pin1, int pin2, unsigned long t) {
  digitalWrite(pin1, LOW);
  digitalWrite(pin2, HIGH);
  delay(t);
  digitalWrite(pin1, LOW);
  digitalWrite(pin2, LOW);
}

void allOff() {
  digitalWrite(motorL1, LOW);
  digitalWrite(motorL2, LOW);
  digitalWrite(motorR1, LOW);
  digitalWrite(motorR2, LOW);
  digitalWrite(motorD1, LOW);
  digitalWrite(motorD2, LOW);
  digitalWrite(motorS1, LOW);
  digitalWrite(motorS2, LOW);
}

// Shuffle motors
void runShuffleLeft()  { motorForward(motorL1, motorL2, shuffleRunTime); }
void runShuffleRight() { motorForward(motorR1, motorR2, shuffleRunTime); }
void runAlternate() {
  while (true) {
    if (!digitalRead(butStart)) break;
    runShuffleLeft();
    allOff();
    delay(shuffleDelay);
    if (!digitalRead(butStart)) break;
    runShuffleRight();
    allOff();
    delay(shuffleDelay);
  }
}

// Deal motor
void runDealForward()  { motorForward(motorD1, motorD2, dealForwardTime); }
void runDealBackward() { motorBackward(motorD1, motorD2, dealBackwardTime); }

// Shooter motor
void shooterOn()       { digitalWrite(motorS1, HIGH); digitalWrite(motorS2, LOW); }
void shooterOff()      { digitalWrite(motorS1, LOW);  digitalWrite(motorS2, LOW); }

// =======================================================
//                        SENSORS
// =======================================================

bool leftHasCards()  { return analogRead(phoLeft)  < sensorThreshold; }
bool rightHasCards() { return analogRead(phoRight) < sensorThreshold; }

// =======================================================
//                     LCD ANIMATIONS
// =======================================================

void lcdShufflingAnimation() {
  lcd.clear();
  lcd.print("Shuffling...");
  Serial.println("Shuffling animation");

  for (int i = 0; i < 3; i++) {
    lcd.setCursor(0, 1); lcd.print(".   "); delay(250);
    lcd.setCursor(0, 1); lcd.print("..  "); delay(250);
    lcd.setCursor(0, 1); lcd.print("..."); delay(250);
  }
}

void lcdDealingAnimation() {
  lcd.clear();
  lcd.print("Dealing cards");
  Serial.println("Dealing animation");

  for (int i = 0; i < 5; i++) {
    lcd.setCursor(0, 1); lcd.print(">");   delay(150);
    lcd.setCursor(0, 1); lcd.print(">>");  delay(150);
    lcd.setCursor(0, 1); lcd.print(">>>"); delay(150);
    lcd.setCursor(0, 1); lcd.print("    "); delay(150);
  }
}

void updateLCD() {
  lcd.clear();
  lcd.setCursor(0,0); lcd.print("Players: "); lcd.print(numPlayers);
  lcd.setCursor(0,1); lcd.print("Cards:   "); lcd.print(numCards);
}

// =======================================================
//                     STEPPER CONTROL
// =======================================================

/*
void setSpeed(int spd) {
  stepperSpeed = constrain(spd, 200, 1800);
  stepper.setMaxSpeed(stepperSpeed);
}
*/

void rotateDegrees(float degrees) {
  long totalSteps = degreesToSteps(degrees);
  // Set direction
  digitalWrite(DIR_PIN, (totalSteps > 0) ? HIGH : LOW);
  long steps = abs(totalSteps);
  // Enable driver
  digitalWrite(ENABLE_PIN, LOW);
  // Step loop
  for (long i = 0; i < steps; i++) {
    digitalWrite(STEP_PIN, HIGH);
    delayMicroseconds(STEP_DELAY_US);
    digitalWrite(STEP_PIN, LOW);
    delayMicroseconds(STEP_DELAY_US);
  }

  // Disable holding torque (prevents heat)
  digitalWrite(ENABLE_PIN, HIGH);
}

// ------------------------------------------------------
// Convert degrees to steps
// ------------------------------------------------------
long degreesToSteps(float degrees) {
  float stepsPerDegree = (STEPS_PER_REV * MICROSTEPPING) / 360.0;
  return (long)(degrees * stepsPerDegree);
}

/*
void rotateDegrees(float degrees) {
  float diff = degrees;
  if (diff > 180) diff -= 360;
  if (diff < -180) diff += 360;

  long steps = lround(diff * stepsPerDegree);
  stepper.move(steps);
  currentAngle += diff;

  if (currentAngle >= 360) currentAngle -= 360;
  if (currentAngle < 0)    currentAngle += 360;
}
*/

// =======================================================
//                   SERIAL TEST MODE
// =======================================================

void runTestMode() {
  lcd.clear();
  lcd.setCursor(0,0);
  lcd.print("TEST MODE");
  lcd.setCursor(0,1);
  lcd.print("Serial Control");

  Serial.println("\n===== TEST MODE =====");
  Serial.println("Shuffle motors:");
  Serial.println("  L1 = Left FORWARD");
  Serial.println("  R1 = Right FORWARD");
  Serial.println("  A = Alternating");
  //Serial.println("  X = Motors Off");
  Serial.println("Deal motor:");
  Serial.println("  D1 = Deal FORWARD");
  Serial.println("  DB = Deal BACKWARD");
  Serial.println("Shooter:");
  Serial.println("  S1 = Shooter ON");
  Serial.println("  S0 = Shooter OFF");
  Serial.println("Stepper:");
  Serial.println("  CW / CCW");
  Serial.println("=======================");

  // Keep test-mode responsive while ensuring stepper gets the time it needs
  while (true) {
    if (!digitalRead(butPlayUp)){
      break;
    }

    // call run() frequently so stepper can move if there's distance to go
    //stepper.run();

    if (Serial.available()) {
      String cmd = Serial.readStringUntil('\n');
      cmd.trim();

      Serial.print("CMD RECEIVED: ");
      Serial.println(cmd);

      // Shuffle
      if (cmd == "L1") runShuffleLeft();
      if (cmd == "R1") runShuffleRight();
      if (cmd == "A")  runAlternate();
      //if (cmd == "X")  allOff();

      // Deal
      if (cmd == "D1") runDealForward();
      if (cmd == "DB") runDealBackward();

      // Shooter
      if (cmd == "S1") shooterOn();
      if (cmd == "S0") shooterOff();

      // Stepper - schedule movement then actively run until complete
      if (cmd == "CW") {
        //enableStepper();
        rotateDegrees(90);
        //while (stepper.distanceToGo() != 0) stepper.run();
        // optionally disableStepper(); // uncomment if you want driver disabled after move
      }
      if (cmd == "CCW") {
        //enableStepper();
        rotateDegrees(-90);
       // while (stepper.distanceToGo() != 0) stepper.run();
        // optionally disableStepper();
      }

      if (cmd.startsWith("SPD")) {
        int sp = cmd.substring(3).toInt();
        //setSpeed(sp);
      }

      if (cmd == "READ") {
        Serial.print("Left Sensor  = ");  Serial.println(analogRead(phoLeft));
        Serial.print("Right Sensor = "); Serial.println(analogRead(phoRight));
      }
    }
  }
}

// =======================================================
//                        SETUP
// =======================================================

void setup() {
  Serial.begin(9600);
  randomSeed(analogRead(randPin));

  pinMode(motorL1, OUTPUT);
  pinMode(motorL2, OUTPUT);
  pinMode(motorR1, OUTPUT);
  pinMode(motorR2, OUTPUT);
  pinMode(motorD1, OUTPUT);
  pinMode(motorD2, OUTPUT);
  pinMode(motorS1, OUTPUT);
  pinMode(motorS2, OUTPUT);

  pinMode(butPlayUp,   INPUT_PULLUP);
  pinMode(butPlayDown, INPUT_PULLUP);
  pinMode(butCardUp,   INPUT_PULLUP);
  pinMode(butCardDown, INPUT_PULLUP);
  pinMode(butStart,    INPUT_PULLUP);

  pinMode(STEP_PIN, OUTPUT);
  pinMode(DIR_PIN, OUTPUT);
  pinMode(ENABLE_PIN, OUTPUT);

  pinMode(MS1_PIN, OUTPUT);
  pinMode(MS2_PIN, OUTPUT);
  pinMode(MS3_PIN, OUTPUT);

  // FULL STEP → lowest current mode
  digitalWrite(MS1_PIN, LOW);
  digitalWrite(MS2_PIN, LOW);
  digitalWrite(MS3_PIN, LOW);

  // Start with driver disabled (cool)
  digitalWrite(ENABLE_PIN, HIGH);

  lcd.begin(16,2);

  //stepper.setMaxSpeed(stepperSpeed);
  //stepper.setAcceleration(800);

  allOff();
  updateLCD();
}

// =======================================================
//                        LOOP
// =======================================================

void loop() {
  allOff();

  if (Serial.available()) {
    String cmd = Serial.readStringUntil('\n');
    cmd.trim();
    if (cmd == "TEST") runTestMode();
  }

  if (!digitalRead(butPlayUp)) { numPlayers++; updateLCD(); delay(200); }
  if (!digitalRead(butPlayDown)) { if (numPlayers > 1) numPlayers--; updateLCD(); delay(200); }

  if (!digitalRead(butCardUp)) { numCards++; updateLCD(); delay(200); }
  if (!digitalRead(butCardDown)) { if (numCards > 1) numCards--; updateLCD(); delay(200); }

  if (!digitalRead(butStart)) {
    lcdShufflingAnimation();

    bool left  = leftHasCards();
    bool right = rightHasCards();

    // --- SHUFFLING ---
    /*
    while (left || right) {
      if (!digitalRead(butStart)){
        break;
      }
      if (left && right) {
        (random(0,2) == 0) ? runShuffleLeft() : runShuffleRight();
      } else if (left)  runShuffleLeft();
      else if (right) runShuffleRight();

      delay(shuffleDelay);
      left  = leftHasCards();
      right = rightHasCards();
    }
    */
    runAlternate();

    // --- DEALING ---
    lcdDealingAnimation();

    float angle = 360.0 / numPlayers;

    runDealBackward();
    shooterOn();

    int remaining = numCards;
    while (true) {
      if (!digitalRead(butStart)){
        break;
      }
      runDealForward();
      delay(dealDelay);
      runDealBackward();
      delay(shuffleDelay);
      //rotateDegrees(angle);
      Serial.println("rotating");
      digitalWrite(motorD1, LOW);
      digitalWrite(motorD2, LOW);
    }
    /*
    while (remaining > 0) {
      if (!digitalRead(butStart)){
        break;
      }
      runDealForward();
      rotateDegrees(angle);
      remaining--;
    }
    */
    shooterOff();

    lcd.clear();
    lcd.setCursor(0,0);
    lcd.print("  Finished!");
    delay(1500);

    updateLCD();
  }

  // Ensure non-blocking motion continues if scheduled elsewhere
  //stepper.run();
}
